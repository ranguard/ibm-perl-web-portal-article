<h2>Summary:</h2>
<p>
<em>Use Perl as a simple and powerful framework for your web site; this article will demonstrate how easy it is to get going and then customise a Plack application for your own needs.</em>
</p><p>
Perl has been through a revolution over the last few years - 'Modern Perl', based on experienced and effective Perl 5 programmers knowledge, is about using the languages idioms and taking advantage of the best of <a href="http://search.cpan.org">CPAN</a>.
</p><p>
We will be demonstrating how to create a web site using <a href="http://search.cpan.org/dist/Plack/">Plack</a>, a 'Moden Perl' tool. Most Perl frameworks work with (or are migrating to) Plack, therefor it will be easy to integrate one of those later should you want to.
</p><p>
Plack is Perl superglue for web frameworks and web servers. Plack sits between your code (whether you use a web framework or not) and the web server (Apache, Starman, etc). This means that you (and your framework) do not need to worry about specifics of a web server, and vice-versa.
</p><p>
Let's get you setup... we are going to use <a href="http://search.cpan.org/dist/App-cpanminus/bin/cpanm">cpanm</a> (from <a href="http://search.cpan.org/dist/App-cpanminus/">App::cpanminus</a>) to download and install modules into your <a href="http://search.cpan.org/dist/local-lib/lib/local/lib.pm">local::lib</a> (so you do not need root access)
</p><p>
<h2>Initial setup:</h2>
<pre>
# archive of any existing cpan configuration
mv ~/.cpan ~/.cpan_original
 
# Then one of the following:

# if you can run wget
wget -O - http://cpanmin.us/ | perl - local::lib App::cpanminus && echo 'eval $(perl -I$HOME/perl5/lib/perl5 -Mlocal::lib)' >> ~/.bashrc && . ~/.bashrc
 
# OR if you can run curl
curl -L http://cpanmin.us/ | perl - local::lib App::cpanminus && echo 'eval $(perl -I$HOME/perl5/lib/perl5 -Mlocal::lib)' >> ~/.bashrc && . ~/.bashrc
 
# otherwise, download the contents of http://cpanmin.us to a file called cpanmin.us, make it executable and then run: 
./cpanmin.us local::lib App::cpanminus && echo 'eval $(perl -I$HOME/perl5/lib/perl5 -Mlocal::lib)' >> ~/.bashrc && . ~/.bashrc
</pre>
</p><p>
<h2>Setting up Plack</h2>
<p>
<pre>
    cpanm Plack
    # Please also run this as we will us it in the demonstration later
    cpanm Plack::Middleware::TemplateToolkit
</pre>
The <code>perl5</code> folder in your home directory will now have all the modules you need.
The next step is to create a .psgi configuration file which will allow us to return a
web page.
</p>
<p>

<a href="psgi_examples/1.psgi">1.psgi</a>
<pre>
    #!/usr/bin/env perl

    # Tell Perl where our lib is (ALWAYS use this)
    use lib "$ENV{HOME}/perl5/lib/perl5";

    # local::lib sets everything up correctly from  (ALWAYS use this)
    use local::lib;

    # ensure we declare everything correctly (ALWAYS use this)
    use strict;

    # Give us diagnostic warnings where possible (ALWAYS use this)
    use warnings;

    # Allow us to build our application
    use Plack::Builder;

    # A basic app
    my $default_app = sub {
        my $env = shift;
        return [
            200,    # HTTP Status code
            [ 'Content-Type' => 'text/html' ],    # HTTP Headers,
            ["All is good"]                       # Content
        ];
    };

    # Return the builder
    return builder {
        $default_app;
    }
</pre>
Save this to a file, called 1.psgi, then use the <a href="http://search.cpan.org/dist/Plack/scripts/plackup">plackup</a> command to start your web server from the command line as follows: 
<pre>
    plackup 1.psgi
</pre>
You will see:
<pre>
    HTTP::Server::PSGI: Accepting connections at http://SERVER_IP:5000/
</pre>
Using your web browser, go to http://SERVER_IP:5000/, if you are developing
on your desktop computer then <a href="http://localhost:5000/">http://localhost:5000/</a> will work.
You should now see a page with 'All is good'. In fact if you go to <em>any</em> page this is what you will see,
e.g. <a href="http://localhost:5000/any_page.html">http://localhost:5000/any_page.html</a> because
we are always returning this, irrespective of the request.
</p><p>
You will notice that on the command line you can see the access logs for the web server, this
is because Plack defaults to development mode and turns on a few extra middleware layers for you,
specifically <a href="http://search.cpan.org/dist/Plack/lib/Plack/Middleware/AccessLog.pm">AccessLog</a>, <a href="http://search.cpan.org/dist/Plack/lib/Plack/Middleware/StackTrace.pm">StackTrace</a> and <a href="http://search.cpan.org/dist/Plack/lib/Plack/Middleware/Lint.pm">Lint</a>.
</p><p>
To see StackTrace in operation comment out line 27 of 1.psgi by adding a hash (#) in front of it:
<pre>
    # ["All is good"]                       # Content
</pre>
Restart your plackup command (type Ctrl+C to stop the process, then run <code>plackup 1.psgi</code> to start it).
Now in your web browser go to <a href="http://localhost:5000/">http://localhost:5000/</a> again and you will see
a StackTrace of the error. Note the main error message at the top of the page "response needs to be 3 element array, or 2 element in streaming". You can then follow each step of the trace, click on the 'Show function arguments' and 'Show lexical variables' links under any section of the trace to help debug the issue.
</p>
<p>
Remove the # and restart, so we have a working .psgi file again.
</p>
<h2>Development</h2>
<p>
There are several command line arguments to the plackup command, running <code>perldoc plackup</code> command will
show you the documentation. The most used is '-r' or '--reload' this tells plackup to monitor you .psgi file
(if you have a 'lib' directory along side your .psgi file it will also be monitored).
<pre>
    plackup -r 1.psgi
</pre>
</p>

<h2>Extending your application</h2>
<p>
    Plack already has many useful applications which you may want to integrate with your web portal, here for example
    we are using <a href="http://search.cpan.org/dist/Plack/lib/Plack/App/Directory.pm">Plack::App::Directory</a> to
    get a directory listing and to serve it's content as static files. We will use <a href="http://search.cpan.org/dist/Plack/lib/Plack/App/URLMap.pm">Plack::App::URLMap</a> to choose which URL we want to 'mount' this application on.
</p>
<p>
    <a href="psgi_examples/2.psgi">2.psgi</a>
    
<pre>
    #!/usr/bin/env perl

    use lib "$ENV{HOME}/perl5/lib/perl5";
    use local::lib;

    use strict;
    use warnings;
    use Plack::Builder;

    # 'mount' applications on specific URLs
    use Plack::App::URLMap;

    # Get directory listings and serve files
    use Plack::App::Directory;

    my $default_app = sub {
        my $env = shift;
        return [ 200, [ 'Content-Type' => 'text/html' ], ["All is good"] ];
    };

    # Get the Directory app, configured with a root directory
    my $dir_app = Plack::App::Directory->new( { root => "/tmp/" } )->to_app;

    # Create a mapper object
    my $mapper = Plack::App::URLMap->new();

    # mount our apps on urls
    $mapper->mount('/'    => $default_app);
    $mapper->mount('/tmp' => $dir_app);

    # extract the new overall app from the mapper
    my $app = $mapper->to_app();

    # Return the builder
    return builder {
        $app;
    }
</pre>
If you visit <a href="http://localhost/tmp/">http://localhost/tmp/</a> in your browser
you will now see a directory listing of '/tmp/' - also not <a href="http://localhost/anything_else.html">http://localhost/anything_else.html</a> still hits
the default_app which we built.
</p>
<p>
    There are many <a href="http://search.cpan.org/search?query=Plack::App&mode=module">Plack::Apps</a> and <a href="http://search.cpan.org/search?query=Plack::Middleware&mode=module">Plack::Middleware</a> modules available to 
help with common tasks. We are going to look at <a href="http://search.cpan.org/dist/Plack-Middleware-TemplateToolkit/">
Plack::Middleware::TemplateToolkit</a>, which parses files through the templating engine <a href="http://search.cpan.org/dist/Template-Toolkit/">Template-Toolkit</a> (TT). Images and other static content should not
go through TT, so we are going to configure <a href="http://search.cpan.org/dist/Plack/lib/Plack/Middleware/Static.pm">Plack::Middleware::Static</a> to directly
serve files with specific extensions. On top of this we want to have a nice looking page when there is a 404 (file not found), for this we will use <a href="http://search.cpan.org/dist/Plack/lib/Plack/Middleware/ErrorDocument.pm">Plack::Middleware::ErrorDocument</a>.
All the code we need to add is as follows:

<pre>
    
    # A link to your htdocs root folder
    my $root = '/path/to/htdocs/';
    
    # Create a new template toolkit application (which we will default to)
    my $default_app = Plack::Middleware::TemplateToolkit->new(
        INCLUDE_PATH => $root,    # Required
    )->to_app();
    
    return builder {

        # Page to show when requested file is missing
        # this will not be processes with TT
        enable "Plack::Middleware::ErrorDocument",
            404 => "$root/page_not_found.html";

        # These files can be served directly
        enable "Plack::Middleware::Static",
            path => qr{[gif|png|jpg|swf|ico|mov|mp3|pdf|js|css]$},
            root => $root;

        # Our application
        $default_app;
    }
    
</pre>






</p>
<h2>Further resources</h2>
<p>
Perl has a vast number of modules available for use on
<a href="http://search.cpan.org/">CPAN</a>. The only real problem
being to work out which are best to use. A good place to start is
 <a href="http://search.cpan.org/dist/Task-Kensho/">Task::Kensho</a>
 which recommends current best practices.
</p><p>






</p><p>

<a href="http://www.perl.org/about/whitepapers/">Perl Whitepapers</a>
</p>
